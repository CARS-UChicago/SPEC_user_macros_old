############################################################################
## epics_PILATUS_count  March 2007 SKG
## use to trigger a PILATUS Detetor
## To read the rois for the PILATUS see the user_getcounts macro
############################################################################ 

# globals
global PILATUS_PREFIX
global PILATUS_DET_PREFIX
global PILATUS_ROI_PREFIX
global PILATUS_STATS_PREFIX


global PILATUS_IMAGE_DIR
global PILATUS_WRITE_FILES

if( unset("PILATUS_PREFIX") )       PILATUS_PREFIX = ""
if( unset("PILATUS_DET_PREFIX") )   PILATUS_DET_PREFIX = ""
if( unset("PILATUS_ROI_PREFIX") )   PILATUS_ROI_PREFIX = ""
if( unset("PILATUS_STATS_PREFIX") ) PILATUS_STATS_PREFIX = ""
if( unset("PILATUS_IMAGE_DIR") )    PILATUS_IMAGE_DIR = ""
if( unset("PILATUS_WRITE_FILES") )  PILATUS_WRITE_FILES = FALSE


# need some more globals (kludge)
global    PILATUS_STATS_PV   
global    PILATUS_STATS_ARRAY_PV 
global    PILATUS_STATS_ARRAY_START_PV
global    PILATUS_STATS_ARRAY_NUSE_PV
global    PILATUS_STATS_ARRAY_ACQ_PV
global    PILATUS_STATS_BgdWidth_PV
global    PILATUS_IMGPATH_PV
global    PILATUS_FNAME_PV
global    PILATUS_FILENUMBER_PV
global    PILATUS_FILEFORMAT_PV
global    PILATUS_EXPSRTM_PV
global    PILATUS_NFRAME_PV
global    PILATUS_EXPPRD_PV
global    PILATUS_NEXPFRM_PV
global    PILATUS_ACQ_PV
global    PILATUS_ARMED_PV
global    PILATUS_ABORT_PV
global    PILATUS_ACQMODE_PV
global    PILATUS_READOUT_TIME

global    PILATUS_ROI_MinX_PV
global    PILATUS_ROI_SizeX_PV
global    PILATUS_ROI_MinY_PV
global    PILATUS_ROI_SizeY_PV

global    PILATUS_Hp
global    PILATUS_Hw
global    PILATUS_Vp
global    PILATUS_Vw
global    PILATUS_MOUNT
global    PILATUS_SPEC_MOUNT

###############################################################
def _setup_img '{
     local j, str
          
     # PILATUS_PREFIX detector name i.e. (GSE-PILATUS1:)
     if ( PILATUS_PREFIX == "") PILATUS_PREFIX = "GSE-PILATUS1:"
     PILATUS_PREFIX = getsval("Enter PILATUS detector name i.e. GSE-PILATUS1:",PILATUS_PREFIX)

     # PILATUS_DET_PREFIX is the pv used by areaDetector to identify a specific detector.
     # When only one detector is used it is usally (cam1:)
     if ( PILATUS_DET_PREFIX == "") PILATUS_DET_PREFIX = "cam1:"
     PILATUS_DET_PREFIX = getsval("Enter PILATUS specific detector name i.e. cam1:",PILATUS_DET_PREFIX)

     # PILATUS_ROI_PREFIX is the pv used by areaDetector to identify a specific ROI plugin.
     # The first ROI plugin is usally (ROI1:)
     if ( PILATUS_ROI_PREFIX == "") PILATUS_ROI_PREFIX = "ROI1:"
     PILATUS_ROI_PREFIX = getsval("Enter PILATUS ROI plugin name i.e. ROI1:",PILATUS_ROI_PREFIX)
     
     # PILATUS_STATS_PREFIX is the pv used by areaDetector to identify a specific a statistics plugin.
     # The first statistics plugin is usally (Stats1:)
     if ( PILATUS_STATS_PREFIX == "") PILATUS_STATS_PREFIX = "Stats1:"
     PILATUS_STATS_PREFIX = getsval("Enter PILATUS statistics plugin name i.e. STATS1:",PILATUS_STATS_PREFIX)
     
     if (PILATUS_MOUNT == "") PILATUS_MOUNT = "/home/det/cars5/Data"
     PILATUS_MOUNT = getsval("Enter mount point relative to camserver home directory",PILATUS_MOUNT)
     if (PILATUS_SPEC_MOUNT == "") PILATUS_SPEC_MOUNT = "cars5/Data"
     PILATUS_SPEC_MOUNT = getsval("Enter mount point relative to spec home directory",PILATUS_SPEC_MOUNT) 


     if ( PILATUS_Hp == "") PILATUS_Hp = 234
     if ( PILATUS_Hp == "") PILATUS_Hw = 2000
     if ( PILATUS_Hp == "") PILATUS_Vp = 103
     if ( PILATUS_Hp == "") PILATUS_Vw = 2000
     PILATUS_Hp = getval("Enter PILATUS center horz pixel",PILATUS_Hp)
     PILATUS_Hw = getval("Enter PILATUS horz roi width in microns",PILATUS_Hw)
     PILATUS_Vp = getval("Enter PILATUS center vert pixel",PILATUS_Vp)
     PILATUS_Vw = getval("Enter PILATUS vert roi width in microns",PILATUS_Vw)	

     # rois pvs
     PILATUS_STATS_PV             = PILATUS_PREFIX PILATUS_STATS_PREFIX "Net_RBV"
     PILATUS_STATS_ARRAY_PV       = PILATUS_PREFIX PILATUS_STATS_PREFIX "NetArray"
     PILATUS_STATS_ARRAY_START_PV = PILATUS_PREFIX PILATUS_STATS_PREFIX "NetArrayEraseStart"
     PILATUS_STATS_ARRAY_NUSE_PV  = PILATUS_PREFIX PILATUS_STATS_PREFIX "NetArray.NUSE"
     PILATUS_STATS_ARRAY_ACQ_PV   = PILATUS_PREFIX PILATUS_STATS_PREFIX "NetArray.ACQG"
     PILATUS_STATS_BgdWidth_PV    = PILATUS_PREFIX PILATUS_STATS_PREFIX "BgdWidth"
     PILATUS_IMGPATH_PV         = PILATUS_PREFIX PILATUS_DET_PREFIX "FilePath"
     PILATUS_FNAME_PV           = PILATUS_PREFIX PILATUS_DET_PREFIX "FileName"
     PILATUS_FILENUMBER_PV      = PILATUS_PREFIX PILATUS_DET_PREFIX "FileNumber"
     PILATUS_FILEFORMAT_PV      = PILATUS_PREFIX PILATUS_DET_PREFIX "FileTemplate"
     PILATUS_EXPSRTM_PV         = PILATUS_PREFIX PILATUS_DET_PREFIX "AcquireTime"
     PILATUS_NFRAME_PV          = PILATUS_PREFIX PILATUS_DET_PREFIX "NumImages"
     PILATUS_EXPPRD_PV          = PILATUS_PREFIX PILATUS_DET_PREFIX "AcquirePeriod"
     PILATUS_NEXPFRM_PV         = PILATUS_PREFIX PILATUS_DET_PREFIX "NumExposures"
     PILATUS_ACQ_PV             = PILATUS_PREFIX PILATUS_DET_PREFIX "Acquire"
     PILATUS_ARMED_PV           = PILATUS_PREFIX PILATUS_DET_PREFIX "Armed"
     PILATUS_ABORT_PV           = PILATUS_PREFIX PILATUS_DET_PREFIX "Acquire"
     PILATUS_ACQMODE_PV         = PILATUS_PREFIX PILATUS_DET_PREFIX "TriggerMode"
     PILATUS_THRESHOLD_PV       = PILATUS_PREFIX PILATUS_DET_PREFIX "ThresholdEnergy"
     PILATUS_ROI_MinX_PV        = PILATUS_PREFIX PILATUS_ROI_PREFIX "MinX"
     PILATUS_ROI_SizeX_PV       = PILATUS_PREFIX PILATUS_ROI_PREFIX "SizeX"
     PILATUS_ROI_MinY_PV        = PILATUS_PREFIX PILATUS_ROI_PREFIX "MinY"
     PILATUS_ROI_SizeY_PV       = PILATUS_PREFIX PILATUS_ROI_PREFIX "SizeY"

     # Set Monitors. We tried this but it is not working. Results in a ca error PJE 1/22/08
     # epics_par(PILATUS_ARMED_PV,"monitor_set")
     # epics_par(PILATUS_ACQ_PV,"monitor_set")

     # Read the value of the threshold energy and test to see
     # if the pilatus IOC is running
     thresholdEnergy = epics_get(PILATUS_THRESHOLD_PV)
     if(thresholdEnergy == ""){
        # Could not connect to the pilatus IOC cleanup and exit
        print PILATUS_PREFIX " IOC is not comunicating or bad PV"
        img_off
        exit
     } 
     else {
       print "The Pilatus threshold Energy is set to: " thresholdEnergy
     }

     # Build directory name. 
     # Here we build the Pilatus file director.  It is broken up into a 
     # Pilatus mount point so that it is easy to change the disk that 
     # the data is saving to while preserving the directory structure 
     # of the spec file location on that disk.  For example we can use 
     # the local disk on the computer controlling the Pilatus and build 
     # a data tree  that matches the one on cars5.
     
     dstr = split(CWD,splt_dstr,PILATUS_SPEC_MOUNT)
     PILATUS_IMAGE_DIR =  sprintf("%s%s/images/",PILATUS_MOUNT, splt_dstr[1])
     #PILATUS_IMAGE_DIR = sprintf("%s/images",PILATUS_IMAGE_DIR)
     print "Pilatus Image directory = ", PILATUS_IMAGE_DIR
     array _temp[256]
     _temp = PILATUS_IMAGE_DIR
     # We set the path above in _setup_img.  That way users can change it if they want.
     epics_put(PILATUS_IMGPATH_PV,_temp, 1)

     # Time needed to read out the Pilatus
     PILATUS_READOUT_TIME = 0.004

      
     #Calc iroi based on pixel center and width in mm.
     img_slit PILATUS_Hw PILATUS_Vw
    
     ct

}'

#def set_img_roi '{
#
# # prompt for roi vals and post to roi xy pvs
#
#}'


##############################################################
def _img_on '{ 

   if (PILATUS_PREFIX == ""){
       eprint "PILATUS_PREFIX unset, run setup_img first!"
       exit
   }

   if ($# < 1){
      PILATUS_WRITE_FILES = getval("Enter 1 to write image files, 0 for off",PILATUS_WRITE_FILES)
   } else {
      PILATUS_WRITE_FILES = $1
      print "PILATUS_WRITE_FILES=", PILATUS_WRITE_FILES
   }

}'

def img_cnt ' epics_pilatus_count '

########################################################
##
## triggers PILATUS detector, manage file storage
## 
def epics_pilatus_count '{
     # local cnt_time_val, sc_pv, PILATUS_PREFIX
     # local pilatus_prtm_pv, pilatus_expsr_pv, pilatus_status_pv   

     if ($# != 1){
         cnt_time_val = 1
     } else {
         cnt_time_val = $1
     } 

     if (PILATUS_PREFIX == ""){
         eprint "PILATUS_PREFIX unset"
         exit
     }

     # base pv names, assume scaler 0 is an epics device
     # thats being used for all the scaler detectors
     sc_pv = counter_par(0,"device_id");

     # preset time and count for scaler
     sc_prtm_pv = sprintf("%s.%s",sc_pv,"TP") 
     sc_cnt_pv  = sprintf("%s.%s",sc_pv,"CNT")

     # Call macro that creates and set the Pilatus path and filename
     img_full_filename

     # Setup exposure time, collection mode and number of frames
     epics_put(PILATUS_FILENUMBER_PV,NPTS, 1)
     epics_put(PILATUS_NFRAME_PV, 1, 1)
     epics_put(PILATUS_ACQMODE_PV,0, 1)  # Internal trigger
     epics_put(PILATUS_EXPSRTM_PV,cnt_time_val, 1)
     epics_put(PILATUS_NEXPFRM_PV, 1, 1)

     epics_put(sc_prtm_pv,cnt_time_val, 1)
     # user pre count stuff
     user_precount

     # hit the triggers
     epics_put(PILATUS_ACQ_PV,1)

     epics_put(sc_cnt_pv,1)

     # wait for scaler and Pilatus AQG to finish
     status     = 1
     sc_done    = FALSE
     img_done   = FALSE
     data_done  = FALSE
     while(status){
         # is the scalar done
        if (epics_get(sc_cnt_pv)=="Done"){
             sc_done = TRUE;
             #p "scaler done"
        }

        # is the pilatus done
        if (epics_get(PILATUS_ACQ_PV) == "Done"){
            img_done = TRUE;
            #p "image collection done"
        }

        if( (sc_done==TRUE) && (img_done==TRUE)) break;
        sleep(0.01)
     }

    
     # use the get_counts routine to read the scalers
     # note get_counts also calls user_getcounts
     # thats where the rois get read.
     get_counts  
}'

###############################################################
def img_slit '{
     
    if ($# != 2) {
        eprint "\
        Usage:  img_slit  Hw Vw"
        exit
    }		

     PILATUS_Hw = $1
     PILATUS_Vw = $2

print PILATUS_Hw PILATUS_Vw

     #Calc iroi based on pixel center and width in mm.
     _hw = round(PILATUS_Hw/(172.))
     _vw = round(PILATUS_Vw/(172.))

     _minX = PILATUS_Hp - _hw/2.
     _sizeX = _hw
     _minY = PILATUS_Vp - _vw/2.
     _sizeY = _vw

     print "Hw [um] = " _sizeX * 172
     print "Vw [um] = " _sizeY * 172

     print "minX  = " _minX
     print "sizeX = " _sizeX
     print "minY  = " _minY
     print "sizeY = " _sizeY

     epics_put(PILATUS_ROI_MinX_PV,_minX, 1)
     epics_put(PILATUS_ROI_SizeX_PV,_sizeX, 1)
     epics_put(PILATUS_ROI_MinY_PV,_minY, 1)
     epics_put(PILATUS_ROI_SizeY_PV,_sizeY, 1)
     epics_put(PILATUS_STATS_BgdWidth_PV,0, 1)

}'

###############################################################
def img_full_filename '{
# Build the full filename put the Pilatus. Note this is called by "epics_pilatus_count" in this file
# and in the traj_scans1.mac by "traj_build"

     # make file name field newfile_SCAN_N or if not writing files reuse the same name.
     array _temp[256]
     if (PILATUS_WRITE_FILES){  
         dstr = split(DATAFILE,splt_dstr,".")
         # Warning the path below can over flowed the pilatus path string buffer if string longer than 156
         # PJE is including it now as a test.  Need to look into making string buffer larger
         # PJE 2/22/08
         _img_path = sprintf("%s%s/S%.3d/",PILATUS_IMAGE_DIR,splt_dstr[0],SCAN_N)
         img_fname = sprintf("%s_S%.3d",DATAFILE,SCAN_N)
         _fileformat = "%s%s_%.3d.tif"
         #print img_fname
     } else {
         _img_path= PILATUS_IMAGE_DIR 
         img_fname = "000test"
         _fileformat = "%s%s.tif"
         # Delete old files before writing new ones to avoid pilatus reusing old files
         _fullname = CWD "/images/" img_fname "*.tif"
         _cmd = "rm " _fullname
         unix(_cmd)
         #print fullname
     }

     # write to data base fields
     # Need to convert strings to byte array
     # Note: we use the "wait" parameter in epics_put here (new to spec5.7.02) so that
     # it uses ca_put_callback, to know that all PVs have been processed
     # before we start counting.  Use 1 second timeout, will actually be
     # much faster than this unless something is wrong.
     # We set the base path above in _setup_img.  That way users can change it if they want.
     
     # The next three epics_puts tells camserver where to save the image file(s)
     _temp = 0
     _temp = _img_path
     epics_put(PILATUS_IMGPATH_PV,_temp, 1)

     _temp = 0
     _temp = img_fname
     epics_put(PILATUS_FNAME_PV,_temp, 1)

     _temp = 0
     _temp = _fileformat
     epics_put(PILATUS_FILEFORMAT_PV,_temp, 1)
}'
